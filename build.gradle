// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
        kotlin : [
            stdlib: '1.4.32',
            coroutines: '1.5.0-RC'
        ],
        androidx : [
            core : '1.5.0-beta01',
            annotation : '1.3.0-alpha01',
            appcompat : '1.3.0-rc01',
            fragment : '1.3.3',
            lifecycle : '2.4.0-alpha01',
            savedstate: '2.4.0-alpha01',
            media: '1.3.0',
        ],
        compose : [
            core : '1.0.0-beta05',
            activity : '1.3.0-alpha07',
            viewmodel: '1.0.0-alpha04',
        ],
        dagger : [
            core : '2.35.1',
            hilt : '2.35.1',
        ],
        exoplayer : [
            core : '2.13.3',
        ],
        difference : '1.0.0',
        test : [
            junit : '4.13.2',
            espresso : '3.2.0',
        ]
    ]

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha15'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.stdlib}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.0.0"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${versions.dagger.hilt}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    targetSdkVersion = 29
    compileSdkVersion = 29
    encoreMinSdkVersion = 16
    jockeyMinSdkVersion = 21

    deps = [
        kotlin : [
            stdlib : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.stdlib}",
            coroutines : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
        ],
        androidx : [
            core : "androidx.core:core-ktx:${versions.androidx.core}",
            annotation : "androidx.annotation:annotation:${versions.androidx.annotation}",
            appcompat : "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
            fragment : "androidx.fragment:fragment:${versions.androidx.fragment}",
            lifecycle: "androidx.lifecycle:lifecycle-common-java8:${versions.androidx.lifecycle}",
            viewmodel: "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx.lifecycle}",
            livedata: "androidx.lifecycle:lifecycle-livedata-ktx:${versions.androidx.lifecycle}",
            savedstate: "androidx.lifecycle:lifecycle-viewmodel-savedstate:${versions.androidx.savedstate}",
            media : "androidx.media:media:${versions.androidx.media}",
        ],
        compose : [
            ui : "androidx.compose.ui:ui:${versions.compose.core}",
            foundation : "androidx.compose.foundation:foundation:${versions.compose.core}",
            material : "androidx.compose.material:material:${versions.compose.core}",
            animation : "androidx.compose.animation:animation:${versions.compose.core}",
            runtime : "androidx.compose.runtime:runtime:${versions.compose.core}",
            activity : "androidx.activity:activity-compose:${versions.compose.activity}",
            livedata : "androidx.compose.runtime:runtime-livedata:${versions.compose.core}",
            viewmodel: "androidx.lifecycle:lifecycle-viewmodel-compose:${versions.compose.viewmodel}",
        ],
        exoplayer : [
            core : "com.google.android.exoplayer:exoplayer-core:${versions.exoplayer.core}",
            mediasession : "com.google.android.exoplayer:extension-mediasession:${versions.exoplayer.core}"
        ],
        dagger : [
            core : "com.google.dagger:dagger:${versions.dagger.core}",
            compiler : "com.google.dagger:dagger-compiler:${versions.dagger.core}",
            hilt : "com.google.dagger:hilt-android:${versions.dagger.hilt}",
            hiltCompiler : "com.google.dagger:hilt-android-compiler:${versions.dagger.hilt}",
        ],
        difference : [
            multiplatform : "dev.andrewbailey.difference:difference:${versions.difference}"
        ],
        test : [
            junit : "junit:junit:${versions.test.junit}",
            espresso : "androidx.test.espresso:espresso-core:${versions.test.espresso}",
            kotlinTest : "org.jetbrains.kotlin:kotlin-test:${versions.kotlin.stdlib}",
            kotlinJunit : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin.stdlib}",
            coroutinesTest : "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlin.coroutines}",
        ]
    ]
}


allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    repositories {
        google()
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    ktlint {
        android = true
        verbose = true
        disabledRules = [
            "no-empty-class-body",
            "no-blank-line-before-rbrace",
        ]
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
