// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlinVersion = '1.3.50'
    ext.navVersion = '2.2.0-beta01'

    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.1.1"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    targetSdkVersion = 29
    compileSdkVersion = 29

    versions = [
        kotlin : [
            stdlib: kotlinVersion,
            coroutines: '1.3.2'
        ],
        androidx : [
            core : '1.2.0-beta01',
            annotation : '1.1.0',
            appcompat : '1.1.0',
            material : '1.1.0-beta01',
            fragment : '1.2.0-beta02',
            navigation : navVersion,
            recyclerview : '1.1.0-beta05',
            constraintlayout: '2.0.0-beta2',
            media: '1.2.0-alpha01',
        ],
        exoplayer : [
            core : '2.10.8',
        ],
        test : [
            junit : '4.12',
            espresso : '3.2.0',
        ]
    ]

    deps = [
        kotlin : [
            stdlib : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.stdlib}",
            coroutines : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
        ],
        androidx : [
            core : "androidx.core:core-ktx:${versions.androidx.core}",
            annotation : "androidx.annotation:annotation:${versions.androidx.annotation}",
            appcompat : "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
            material : "com.google.android.material:material:${versions.androidx.material}",
            fragment : "androidx.fragment:fragment:${versions.androidx.fragment}",
            navigation : "androidx.navigation:navigation-fragment-ktx:${versions.androidx.navigation}",
            navigationui : "androidx.navigation:navigation-ui-ktx:${versions.androidx.navigation}",
            recyclerview : "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}",
            constraintlayout : "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}",
            media : "androidx.media:media:${versions.androidx.media}",
        ],
        exoplayer : [
            core : "com.google.android.exoplayer:exoplayer-core:${versions.exoplayer.core}",
            mediasession : "com.google.android.exoplayer:extension-mediasession:${versions.exoplayer.core}"
        ],
        test : [
            junit : "junit:junit:${versions.test.junit}",
            espresso : "androidx.test.espresso:espresso-core:${versions.test.espresso}",
        ]
    ]
}


allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    repositories {
        google()
        jcenter()
    }

    ktlint {
        android = true
        verbose = true
        disabledRules = [
            "no-empty-class-body",
            "no-blank-line-before-rbrace",
            "no-wildcard-imports",
        ]
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
